apply plugin: 'com.android.library'

def dataCollectorVersion = 'paypal-data-collector-4.1.2.jar'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
    }
}

repositories {
    flatDir {
        dirs "libs"
    }
}

dependencies {
    api files('libs/' + dataCollectorVersion)
    compileOnly 'androidx.annotation:annotation:1.0.0'

    androidTestImplementation 'androidx.test:runner:1.1.0-beta02'
}

/* maven deploy + signing */
task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    classifier = 'javadoc'
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocsJar
    archives sourcesJar
}

nexusStaging {
    packageGroup = "com.paypal"
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                groupId = 'com.paypal.android.sdk'
                artifactId = 'data-collector'
                version = "${android.defaultConfig.versionName}"

                pom {
                    name = 'data-collector'
                    description = 'Fraud tools for PayPal integrations on Android.'
                }
            }
        }
    }
}
