import groovy.json.JsonOutput
import groovy.json.JsonSlurper

apply plugin: 'com.android.library'

ext {
    configFileText = new File("${projectDir}/otc-config.android.json").getText('UTF-8')
    config = JsonOutput.toJson(new JsonSlurper().parseText(configFileText)).replaceAll("\"", "\\\\\"")
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        buildConfigField "String", "CONFIGURATION", "\"${config}\""
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
    }
}

dependencies {
    compileOnly 'androidx.annotation:annotation:1.0.0'

    api project(':Core')
    api project(':PayPalDataCollector')

    testImplementation 'org.robolectric:robolectric:4.1'
    testImplementation 'org.skyscreamer:jsonassert:1.4.0'
    testImplementation 'org.powermock:powermock-module-junit4:1.6.6'
    testImplementation 'org.powermock:powermock-module-junit4-rule:1.6.6'
    testImplementation 'org.powermock:powermock-api-mockito:1.6.6'
    testImplementation 'org.powermock:powermock-classloading-xstream:1.6.6'
    testImplementation project(':TestUtils')
}

/* maven deploy + signing */
task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    classifier = 'javadoc'
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocsJar
    archives sourcesJar
}

nexusStaging {
    packageGroup = "com.paypal"
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                groupId = 'com.paypal.android.sdk'
                artifactId = 'paypal-one-touch'
                version = "${android.defaultConfig.versionName}"

                pom {
                    name = 'paypal-one-touch'
                    description = 'PayPal SDK for handling app and browser switch based payments.'
                }
            }
        }
    }
}
